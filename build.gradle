import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'org.spongepowered.plugin' version '0.8.1'
    id "net.kyori.blossom" version "1.1.0"
}

apply plugin: 'java-library'

group = 'cz.neumimto'
version = '1.13-SNAPSHOT-10'

sponge {
    plugin {
        meta {
            version = getVersion().toString()
        }
    }
}

blossom {
    replaceToken '@VERSION@', getVersion().toString()
}

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = 'jitpack.io'
        url 'https://jitpack.io'
    }
}

dependencies {
    api 'com.github.NeumimTo:IoC:b14871cd33'
    api 'com.github.NeumimTo:HoconConfigMapper:f8de21c6df'
    api 'org.hibernate:hibernate-entitymanager:5.3.6.Final'

    compileOnly 'com.zaxxer:HikariCP:2.4.1'
    compileOnly 'org.spongepowered:spongeapi:' + project.spongeApiVersion
    compileOnly 'com.github.Mojang:LegacyLauncher:59c38e589b'
}


shadowJar {
    classifier = 'shaded'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

build.dependsOn(shadowJar)


processResources {
    from (sourceSets.main.java) {
        include("*.java")
        filter(ReplaceTokens, tokens: ["@@VERSION@@" : project.version])
    }
}
